PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX coso: <http://w3id.org/coso/v1/contaminoso#>
PREFIX me_egad_data: <http://w3id.org/sawgraph/v1/me-egad-data#>

# Samples from what surface water bodies have a cumulative contamination result of above 20 ppt?

SELECT ?feature ?featureType ?sample ?date (SUM(?value) as ?cumulativeConcentration) ?unit WHERE {
    ?sample a coso:WaterSample; 									# All Water Samples
    		a ?surfaceWater;										# of a particular class (surface water)
    		coso:isSampleOf ?feature;				
    		^coso:analyzedSample ?observation.
    ?feature coso:ofFeatureType/rdfs:label ?featureType. 			# With feature info
    ?observation coso:ofSubstance/rdfs:label ?substanceName; 		# tested for each substance
    			coso:observedTime ?date;							# on what sample date
    			coso:hasResult ?measurement.
    ?measurement qudt:quantityValue/qudt:numericValue ?value; 		# only numeric results (ignore non-detects)
    		coso:measurementUnit ?unit .					 		# in the same unit
    VALUES ?surfaceWater {me_egad_data:DefEGADSurfaceWaterSample}.  # of surface water
    VALUES ?unit {unit:NanoGM-PER-L}.								# measured in ng/l

} GROUP BY ?sample ?feature ?featureType ?date ?unit
HAVING(?cumulativeConcentration > 20)								# Only those with a total concentration over 20 ng/l