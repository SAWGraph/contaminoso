# All surface water and fish tissue sample results from one samplepoint
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX coso: <http://w3id.org/coso/v1/contaminoso#>
PREFIX me_egad_data: <http://w3id.org/sawgraph/v1/me-egad-data#>

select ?materialSample ?observation  (GROUP_CONCAT(DISTINCT(?sampleType); separator=", ") as ?sampleTypes) ?substance ?substanceL ?value ?unit
where  {
        VALUES ?samplecategories{coso:WaterSample coso:AnimalMaterialSample} #expand to fish
        me_egad_data:samplePoint.105221 coso:pointFromFeature/coso:ofFeatureType ?featureType;
            ^coso:fromSamplePoint ?materialSample.   #get samples for the point
        ?observation coso:analyzedSample ?materialSample; #get observations related to the sample
            coso:ofSubstance ?substance;
            coso:hasResult ?measure.
        ?materialSample coso:sampleOfMaterialType ?st; # a sample can have multiple types
    		rdf:type ?samplecategories.
        ?st rdfs:label ?sampleType.
        ?substance rdfs:label ?substanceL.
		
        #Filter to a specific substance: 
		# VALUES ?substance {}.

        ?measure coso:measurementValue ?value;
            coso:measurementUnit ?unit.
        FILTER NOT EXISTS{ ?measure a coso:AggregateContaminantMeasurement} #remove results that are aggregates
} GROUP BY ?materialSample ?observation ?substance ?substanceL ?value ?unit 
LIMIT 100